apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 30
        versionName "1.8"
        resConfigs "en", "ru", "th"

        buildConfigField "boolean", "USE_DB_SNAPSHOT", "false"
    }

    signingConfigs {
        debug {
            storeFile file(KEYSTORE_PATH)
            storePassword KEYSTORE_PASSWORD
            keyAlias "coins"
            keyPassword KEYSTORE_PASSWORD
        }

        release {
            storeFile file(KEYSTORE_PATH)
            storePassword KEYSTORE_PASSWORD
            keyAlias "coins"
            keyPassword KEYSTORE_PASSWORD
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-dev"
            minifyEnabled true
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField "String", "API_URL", "\"http://192.168.1.7:3000/api/v2/\""
        }
        release {
            minifyEnabled true
            useProguard true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            buildConfigField "String", "API_URL", "\"http://91.121.144.57:3000/api/v2/\""
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def projectName = 'coins'
            output.outputFile = file("$project.buildDir/apk/$projectName-$variant.versionName" + '.apk')
        }
    }
}

dependencies {
    // Android support libraries
    compile "com.android.support:appcompat-v7:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:design:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:customtabs:${rootProject.ext.supportLibVersion}"
    compile "com.android.support:cardview-v7:${rootProject.ext.supportLibVersion}"

    // Google Play services split client libs
    compile "com.google.android.gms:play-services-gcm:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-location:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-maps:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.android.gms:play-services-auth:${rootProject.ext.googlePlayServicesVersion}"
    compile "com.google.firebase:firebase-crash:${rootProject.ext.googlePlayServicesVersion}"

    compile 'com.google.maps.android:android-maps-utils:0.4'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.5.0'

    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    compile 'com.jakewharton.timber:timber:4.1.1'

    compile 'com.google.code.gson:gson:2.7'

    compile 'com.google.dagger:dagger:2.8'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.8'

    testCompile 'junit:junit:4.12'
    testCompile 'org.robolectric:robolectric:3.3'
}

apply plugin: 'com.google.gms.google-services'
